apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.netlight.cleanarchitecturesample"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "String", "API_YESNO", '"https://yesno.wtf"'
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

    testOptions {
        animationsDisabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE-notice.md'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Kotlin Coroutines
    def kotlinCoroutines = "1.3.2" // https://github.com/Kotlin/kotlinx.coroutines/releases
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutines"

    // AndroidX https://dl.google.com/dl/android/maven2/index.html
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta2"

    // Material
    implementation "com.google.android.material:material:1.0.0"

    // Room
    def roomVersion = "2.2.0"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Koin
    def koinVersion = '2.0.1' // https://github.com/InsertKoinIO/koin/releases
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"
    implementation "org.koin:koin-android:$koinVersion"

    // Retrofit
    def retrofitVersion = "2.6.1" // https://github.com/square/retrofit/releases
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Coil
    def coilVersion = "0.7.0" // https://github.com/coil-kt/coil/releases
    implementation "io.coil-kt:coil:$coilVersion"
    implementation "io.coil-kt:coil-gif:$coilVersion"

    // ---- Testing ----

    // Kotlin
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // Kotlin Coroutines
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutines"

    // jUnit
    def jUnit5Version = "5.5.2" // https://github.com/junit-team/junit5/releases
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$jUnit5Version"
    testImplementation "org.junit.vintage:junit-vintage-engine:$jUnit5Version"
    testImplementation "org.junit.platform:junit-platform-launcher:1.5.2"

    // AssertJ https://mvnrepository.com/artifact/org.assertj/assertj-core
    testImplementation "org.assertj:assertj-core:3.13.2"

    // AndroidX Test
    androidTestImplementation "androidx.test:core:1.2.0"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"

    // MockK
    def mockkVersion = '1.9.3' // https://github.com/mockk/mockk/releases
    testImplementation "io.mockk:mockk:$mockkVersion"
    androidTestImplementation "io.mockk:mockk-android:$mockkVersion"

    // Koin
    androidTestImplementation "org.koin:koin-test:2.0.1"
}
